OBJECT Codeunit 81000 S04 Actindo Image Handler
{
  OBJECT-PROPERTIES
  {
    Date=04.11.25;
    Time=14:10:32;
    Modified=Yes;
    Version List=S04.01,ACTINDO;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            Test();
          END;

  }
  CODE
  {

    LOCAL PROCEDURE Test@5091706();
    VAR
      Item@5091700 : Record 27;
      recImage@5091701 : Record 5243065;
      S04Utilities@5091702 : Codeunit 60060;
    BEGIN
      {
      WITH recImage DO BEGIN
        RESET();
        SETRANGE("Item No.", '25004');
        SETRANGE("Main Image", TRUE);
          IF FINDSET() THEN BEGIN
          REPEAT
            MESSAGE(S04Utilities.GetWebItemImageFileType(recImage));
          UNTIL recImage.NEXT() = 0;
        END;
      END;
      }
      SetImagesBatch();
    END;

    LOCAL PROCEDURE SendToMiddleware@5091701(payload@5091704 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";endpoint@5091703 : Text) responseText : Text;
    VAR
      ActindoCore@5091702 : Codeunit 50090;
      WebServiceSetup@5091701 : Record 60073;
      WebserviceEndpoint@5091700 : Record 80090;
    BEGIN
      // get webservice setup
      WebServiceSetup.GET('ACTINDO');
      WebserviceEndpoint.GET(WebServiceSetup.Code, endpoint);

      // send request
      ActindoCore.SendRequest('POST', WebserviceEndpoint, payload, {ref}responseText, '');
      EXIT(responseText);
    END;

    PROCEDURE SetImages@5091703(Item@5091700 : Record 27) : Text;
    VAR
      payload@5091701 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";
      responseText@5091702 : Text;
      response@5091703 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";
      JObject@5091704 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";
    BEGIN
      CreateJsonObject(Item, {ref}payload);
      responseText := SendToMiddleware(payload, 'SET_IMAGES');

      response := JObject.Parse(responseText);
      EXIT(response.ToString());
    END;

    LOCAL PROCEDURE SetImagesBatch@5091702();
    VAR
      Item@5091700 : Record 27;
    BEGIN
      WITH Item DO BEGIN
        RESET();
        SETFILTER("Actindo POS ID", '<>%1', '');
          IF FINDSET() THEN BEGIN
          REPEAT
            SetImages(Item);
          UNTIL Item.NEXT() = 0;
        END;
      END;
    END;

    LOCAL PROCEDURE CreateJsonObject@5091705(Item@5091700 : Record 27;VAR payload@5091701 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject");
    VAR
      Convert@5091706 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Convert";
      MemStream@5091705 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.MemoryStream";
      InS@5091704 : InStream;
      Base64txt@5091703 : Text;
      recImage@5091702 : Record 5243065;
      JArrayImgs@5091707 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JArray";
      JArrayPaths@5091708 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JArray";
      JPath@5091710 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";
      JImage@5091711 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JObject";
      prop@5091709 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JProperty";
      i@5091712 : Integer;
      path@5091713 : Text;
      type@5091714 : Text;
      S04Utilities@5091715 : Codeunit 60060;
      JTokenImg@5091716 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JToken";
      JTokenPath@5091717 : DotNet "'Newtonsoft.Json, Version=6.0.0.0, Culture=neutral, PublicKeyToken=30ad4fe6b2a6aeed'.Newtonsoft.Json.Linq.JToken";
      extension@5091718 : Text;
    BEGIN
      {
      "id": 123,
      "images": [{"path": "products/img/xx.jpg", "type": "image/jpeg", "content": "..."}],
      "paths": [{"id": "products/img/xx.jpg"}]
      }

      payload := payload.JObject();
      JArrayImgs := JArrayImgs.JArray();
      JArrayPaths := JArrayPaths.JArray();

      IF Item."Actindo POS ID" = '' THEN
        EXIT;

      payload.Add(prop.JProperty('id', Item."Actindo POS ID"));
      i := 1;
      WITH recImage DO BEGIN
        RESET();
        SETRANGE("Item No.", Item."No.");
        SETRANGE("Main Image", TRUE);
        IF FINDSET() THEN BEGIN
          REPEAT
            CALCFIELDS(Image);
            IF recImage.Image.HASVALUE THEN BEGIN
              recImage.Image.CREATEINSTREAM(InS);

              MemStream := MemStream.MemoryStream();
              COPYSTREAM(MemStream, InS);

              Base64txt := Convert.ToBase64String(MemStream.ToArray());
              extension := S04Utilities.GetWebItemImageFileType(recImage);
              IF extension = '' THEN
                extension := 'jpg';

              type := S04Utilities.GetMimeTypeByFileType(extension);




              IF type <> '' THEN BEGIN
                path := STRSUBSTNO('products/img/%1_%2.%3', Item."Actindo POS ID", i, extension);
                i := i + 1;

                //  JSON Objekt f r Bild
                JImage := JImage.JObject();
                JImage.Add(prop.JProperty('path', path));
                JImage.Add(prop.JProperty('type', type
                ));
                JImage.Add(prop.JProperty('content', Base64txt));
                JTokenImg := JImage;
                JArrayImgs.Add(JTokenImg);

                // JSON Objekt f r Pfad
                JPath := JPath.JObject();
                JPath.Add(prop.JProperty('id', path));
                JTokenPath := JPath;
                JArrayPaths.Add(JTokenPath);
              END;
            END;
          UNTIL recImage.NEXT() = 0;
        END;
      END;

      //main object
      payload.Add('images', JArrayImgs);
      payload.Add('paths', JArrayPaths);
    END;

    BEGIN
    END.
  }
}

